; REQUIRES: spirv-as
; RUN: spirv-as --target-env spv1.0 -o %t.spv %s
; RUN: spirv-val %t.spv
; RUN: llvm-spirv -r -o - %t.spv | llvm-dis | FileCheck %s
               OpCapability Addresses
               OpCapability Kernel
               OpMemoryModel Physical32 OpenCL
               OpEntryPoint Kernel %1 "testFAdd"
               OpName %a "a"
               OpName %b "b"
               OpName %r1 "r1"
               OpName %r2 "r2"
               OpName %r3 "r3"
               OpName %r4 "r4"
               OpName %r5 "r5"
               OpName %r6 "r6"
               OpName %r7 "r7"
               OpName %r8 "r8"
               OpName %r9 "r9"
               OpDecorate %r2 FPFastMathMode None
               OpDecorate %r3 FPFastMathMode NotNaN
               OpDecorate %r4 FPFastMathMode NotInf
               OpDecorate %r5 FPFastMathMode NSZ
               OpDecorate %r6 FPFastMathMode AllowRecip
               OpDecorate %r7 FPFastMathMode Fast
               OpDecorate %r8 FPFastMathMode NotNaN|NotInf
               OpDecorate %r9 FPFastMathMode NotNaN|Fast
       %void = OpTypeVoid
      %float = OpTypeFloat 32
          %5 = OpTypeFunction %void %float %float
          %1 = OpFunction %void None %5
          %a = OpFunctionParameter %float
          %b = OpFunctionParameter %float
          %6 = OpLabel
         %r1 = OpFAdd %float %a %b
         %r2 = OpFAdd %float %a %b
         %r3 = OpFAdd %float %a %b
         %r4 = OpFAdd %float %a %b
         %r5 = OpFAdd %float %a %b
         %r6 = OpFAdd %float %a %b
         %r7 = OpFAdd %float %a %b
         %r8 = OpFAdd %float %a %b
         %r9 = OpFAdd %float %a %b
               OpReturn
               OpFunctionEnd

; CHECK: %r1 = fadd float %a, %b
; CHECK: %r2 = fadd float %a, %b
; CHECK: %r3 = fadd nnan float %a, %b
; CHECK: %r4 = fadd ninf float %a, %b
; CHECK: %r5 = fadd nsz float %a, %b
; CHECK: %r6 = fadd arcp float %a, %b
; CHECK: %r7 = fadd fast float %a, %b
; CHECK: %r8 = fadd nnan ninf float %a, %b
; CHECK: %r9 = fadd fast float %a, %b
