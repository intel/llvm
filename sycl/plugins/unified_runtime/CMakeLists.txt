# PI Unified Runtime plugin library.
#
if (NOT DEFINED UNIFIED_RUNTIME_LIBRARY OR NOT DEFINED UNIFIED_RUNTIME_INCLUDE_DIR)
  include(FetchContent)

  set(UNIFIED_RUNTIME_REPO "https://github.com/oneapi-src/unified-runtime.git")
  set(UNIFIED_RUNTIME_TAG e6343f4cca9a37b17bc63f3a81968ac3f486be8a)

  set(UR_BUILD_ADAPTER_L0 ON)

  if ("cuda" IN_LIST SYCL_ENABLE_PLUGINS)
    set(UR_BUILD_ADAPTER_CUDA ON)
  endif()

  if ("hip" IN_LIST SYCL_ENABLE_PLUGINS)
    set(UR_BUILD_ADAPTER_HIP ON)
  endif()
  set(UMF_ENABLE_POOL_TRACKING ON)
  message(STATUS "Will fetch Unified Runtime from ${UNIFIED_RUNTIME_REPO}")
  FetchContent_Declare(unified-runtime
    GIT_REPOSITORY    ${UNIFIED_RUNTIME_REPO}
    GIT_TAG           ${UNIFIED_RUNTIME_TAG}
  )

  # Disable errors from warnings while building the UR.
  # And remember origin flags before doing that.
  set(CMAKE_CXX_FLAGS_BAK "${CMAKE_CXX_FLAGS}")
  if(WIN32)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /WX-")
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} /WX-")
    # FIXME: Unified runtime build fails with /DUNICODE
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /UUNICODE")
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} /UUNICODE")
    # USE_Z7 forces use of /Z7 instead of /Zi which is broken with sccache
    set(USE_Z7 ON)
  else()
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-error")
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wno-error")
  endif()

  # No need to build tests from unified-runtime
  set(UR_BUILD_TESTS "0" CACHE STRING "0")

  FetchContent_GetProperties(unified-runtime)
  FetchContent_MakeAvailable(unified-runtime)

  # Restore original flags
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS_BAK}")

  add_library(UnifiedRuntimeLoader ALIAS ur_loader)
  add_library(UnifiedRuntimeCommon ALIAS ur_common)
  add_library(UnifiedMallocFramework ALIAS unified_malloc_framework)

  set(UNIFIED_RUNTIME_SOURCE_DIR
    ${unified-runtime_SOURCE_DIR} CACHE PATH "Path to Unified Runtime Headers")
  set(UNIFIED_RUNTIME_INCLUDE_DIR "${UNIFIED_RUNTIME_SOURCE_DIR}/include")
endif()

add_library(UnifiedRuntime-Headers INTERFACE)

target_include_directories(UnifiedRuntime-Headers
  INTERFACE
    "${UNIFIED_RUNTIME_INCLUDE_DIR}"
)

find_package(Threads REQUIRED)

add_sycl_plugin(unified_runtime
  SOURCES
    # These are short-term shared with Unified Runtime
    # The two plugins define a few things differrently so must
    # be built separately. This difference is spelled in
    # their "ur_bindings.hpp" files.
    #
    "ur_bindings.hpp"
    "pi2ur.hpp"
    "pi2ur.cpp"
    # These below belong to Unified Runtime PI Plugin only
    "pi_unified_runtime.hpp"
    "pi_unified_runtime.cpp"
    LIBRARIES
    Threads::Threads
    UnifiedRuntimeLoader
    UnifiedRuntime-Headers
    LevelZeroLoader-Headers # we need for #include <ze_api.h> in common.h
)

add_dependencies(sycl-runtime-libraries ur_adapter_level_zero)
if("hip" IN_LIST SYCL_ENABLE_PLUGINS)
  add_dependencies(sycl-runtime-libraries ur_adapter_hip)
endif()

if ("cuda" IN_LIST SYCL_ENABLE_PLUGINS)
  add_dependencies(sycl-runtime-libraries ur_adapter_cuda)
endif()

if("native_cpu" IN_LIST SYCL_ENABLE_PLUGINS)
  add_sycl_library("ur_adapter_native_cpu" SHARED
    SOURCES
      "ur/ur.cpp"
      "ur/ur.hpp"
      "ur/adapters/native_cpu/adapter.cpp"
      "ur/adapters/native_cpu/command_buffer.cpp"
      "ur/adapters/native_cpu/common.cpp"
      "ur/adapters/native_cpu/common.hpp"
      "ur/adapters/native_cpu/context.cpp"
      "ur/adapters/native_cpu/context.hpp"
      "ur/adapters/native_cpu/device.cpp"
      "ur/adapters/native_cpu/device.hpp"
      "ur/adapters/native_cpu/enqueue.cpp"
      "ur/adapters/native_cpu/event.cpp"
      "ur/adapters/native_cpu/image.cpp"
      "ur/adapters/native_cpu/kernel.cpp"
      "ur/adapters/native_cpu/kernel.hpp"
      "ur/adapters/native_cpu/memory.cpp"
      "ur/adapters/native_cpu/memory.hpp"
      "ur/adapters/native_cpu/platform.cpp"
      "ur/adapters/native_cpu/platform.hpp"
      "ur/adapters/native_cpu/program.cpp"
      "ur/adapters/native_cpu/program.hpp"
      "ur/adapters/native_cpu/queue.cpp"
      "ur/adapters/native_cpu/queue.hpp"
      "ur/adapters/native_cpu/sampler.cpp"
      "ur/adapters/native_cpu/ur_interface_loader.cpp"
      "ur/adapters/native_cpu/usm.cpp"
      "ur/adapters/native_cpu/usm_p2p.cpp"
    INCLUDE_DIRS
      ${sycl_inc_dir}
    LIBRARIES
      UnifiedRuntime-Headers
      Threads::Threads
      sycl
      OpenCL-Headers
  )

  set_target_properties("ur_adapter_native_cpu" PROPERTIES
    VERSION "0.0.0"
    SOVERSION "0"
  )
endif()


if (TARGET UnifiedRuntimeLoader)
  set_target_properties(hello_world PROPERTIES EXCLUDE_FROM_ALL 1 EXCLUDE_FROM_DEFAULT_BUILD 1)
  # Install the UR loader.
  # TODO: this is piggy-backing on the existing target component level-zero-sycl-dev
  # When UR is moved to its separate repo perhaps we should introduce new component,
  # e.g. unified-runtime-sycl-dev.
  install(TARGETS ur_loader
    LIBRARY DESTINATION "lib${LLVM_LIBDIR_SUFFIX}" COMPONENT level-zero-sycl-dev
    ARCHIVE DESTINATION "lib${LLVM_LIBDIR_SUFFIX}" COMPONENT level-zero-sycl-dev
    RUNTIME DESTINATION "bin" COMPONENT level-zero-sycl-dev
  )
endif()

# Install the UR adapters too
install(TARGETS ur_adapter_level_zero
  LIBRARY DESTINATION "lib${LLVM_LIBDIR_SUFFIX}" COMPONENT level-zero-sycl-dev
  ARCHIVE DESTINATION "lib${LLVM_LIBDIR_SUFFIX}" COMPONENT level-zero-sycl-dev
  RUNTIME DESTINATION "bin" COMPONENT level-zero-sycl-dev
)
